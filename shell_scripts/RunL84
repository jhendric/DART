#!/bin/csh
#
# Data Assimilation Research Testbed -- DART
# Copyright 2004, Data Assimilation Initiative, University Corporation for Atmospheric Research
# Licensed under the GPL -- www.gpl.org/licenses/gpl.html
#
# $Id$
#
# script to run an experiment with existing intial conditions
#======================================================================
# Find the project and build it, if need be.
#======================================================================

set SNAME = $0
set clobber
set DART_HOME = ~/DART
set Model = lorenz_84

switch ( $#argv )
   case 1:
      # supplying one argument -- the number of ensemble members
      set NumEns = $1
      breaksw
   case 2:
      # supplying two arguments
      set NumEns = $1
      set DART_HOME = $2
      breaksw
   default:
      echo " "
      echo "usage: $SNAME:t #_ensemble_members [DART_HOME]"
      echo " "
      echo "This script demonstrates how to conduct a DART assimilation experiment for"
      echo "a given model which has already been spun-up and has initial conditions"
      echo "files <perfect_ics> and <filter_ics>."
      echo " "
      echo "'#_ensemble_members' can be no greater than the number of ensemble members "
      echo "in the <filter_ics> initial conditions file."
      echo " "
      echo "'DART_HOME' must point to your 'top-level' DART directory."
      echo "If DART_HOME is not supplied -- it is assumed to be ~/DART."
      echo " "
      exit 1
      breaksw
endsw

#======================================================================
# Query all the environment variables.
# This is only meant to be illustrative.
# It is ABSOLUTELY not comprehensive.
#======================================================================

set obs_error_variance = 1.0
if ( ${?ObsError} ) then
   set obs_error_variance = $ObsError
endif

set num_obs = 1000
set ob_day_1 = 0
set ob_seconds_1 = 43200

if ( ${?NumObs} ) then     ;# if environment var NumObs exists, use it
   set num_obs = $NumObs
endif
if ( ${?ObDay1} ) then
   set ob_day_1 = $ObDay1
endif
if ( ${?ObSec1} ) then
   set ob_seconds_1 = $ObSec1
endif

set cov_inflate = 1.00
set cutoff = 22222222.0

if ( ${?CovInflate} ) then
   set cov_inflate = $CovInflate
endif
if ( ${?Cutoff} ) then
   set cutoff = $Cutoff
endif

#======================================================================
# This block checks to see if input is viable.
#======================================================================

setenv DARTDIR ${DART_HOME}/models/${Model}/work
if ( ! -d ${DARTDIR} ) then                                                     
   echo "${DARTDIR} does not exist ... "
   exit 2
endif

echo "DART project directory is $DART_HOME"
echo "Building Initial Conditions for $Model"
echo "in directory $DARTDIR"
echo "You have 4 seconds to stop this ..."
sleep 1
echo "3"
sleep 1
echo "2"
sleep 1
echo "1"
sleep 1
echo
                                                                                
cd ${DARTDIR}

#======================================================================
# Save any existing namelist ...
# Creating a null namelist just to avoid moving nothing.
#======================================================================

touch nullnamelist$$.nml
touch nullnamelist$$.nml_default

set SAVEME = .NamelistStorage.$$
if ( ! -d $SAVEME ) then
   mkdir ${SAVEME}
endif
foreach FILE ( *.nml *default )
   \mv -f $FILE ${SAVEME}
end
echo 'Saved all existing *.nml into directory '"$SAVEME"
echo 'Will restore and remove the directory at the end.'

#======================================================================
# Create the executables if they do not exist.
#======================================================================

if !( -e create_obs_set_def ) then
   csh mkmf_create_obs_set_def
   make || exit 1
endif
if !( -e create_obs_sequence ) then
   csh mkmf_create_obs_sequence
   make || exit 2
endif
if !( -e perfect_model_obs ) then
   csh mkmf_perfect_model_obs
   make || exit 3
endif
if !( -e filter ) then
   csh mkmf_filter
   make || exit 4
endif
\rm -f *.o *.d *.mod Makefile

#======================================================================
# Create common namelists -- lots of ways to do it.
#======================================================================

cat << ENDofTask00 > utilities.nml
&utilities_nml
   logfilename = "create_obs_set_def.out",
   TERMLEVEL = 1 /
ENDofTask00
   
cat << ENDofTask01 > assim_mod.nml
&assim_model_nml
   binary_restart_files = .false. /
ENDofTask01

cat << ENDofTask02 > model.nml

&model_nml
   a      = 0.25,
   b      = 4.00,
   f      = 8.00,
   g      = 1.25,
   deltat = 0.01   /

ENDofTask02

#======================================================================
# Simulate a particular observing system:
# 1) declare an observation set definition
#======================================================================

echo " "
echo "Running create_obs_set_def ..."
echo " "


echo "set_def.out"          > create_obs_set_def.in ;# output file name
echo "1"                   >> create_obs_set_def.in ;# # of unique observation sets
echo "3"                   >> create_obs_set_def.in ;# # of observations in set 1 (x,y,z)
echo $obs_error_variance   >> create_obs_set_def.in ;# error variance for set 1, obs 1
echo "1"                   >> create_obs_set_def.in ;# 1 == state variable 1 (X)
echo $obs_error_variance   >> create_obs_set_def.in ;# error variance for set 1, obs 2
echo "2"                   >> create_obs_set_def.in ;# 2 == state variable 2 (Y)
echo $obs_error_variance   >> create_obs_set_def.in ;# error variance for set 1, obs 3
echo "3"                   >> create_obs_set_def.in ;# 3 == state variable 3 (Z)

echo '&utilities_nml'                                  > utilities.nml
echo '   logfilename = "create_obs_set_def.out"'      >> utilities.nml
echo '   TERMLEVEL = 1   /'                           >> utilities.nml

cat utilities.nml assim_mod.nml model.nml > input.nml

./create_obs_set_def < create_obs_set_def.in

if ( $status > 0 ) then
   echo "ERROR: running create_obs_set_def bombed ..."
   exit
endif

#======================================================================
# Simulate a particular observing system:
# 2) declare sequences of observation sets
#======================================================================

echo " "
echo "Running create_obs_sequence ..."
echo " "


echo "set_def.out"            > create_obs_sequence.in ;# same filename as 'output' above
echo "1"                     >> create_obs_sequence.in ;# make a "regular" sequence
echo $num_obs                >> create_obs_sequence.in ;# number of observations in set 1
echo $ob_day_1 $ob_seconds_1 >> create_obs_sequence.in ;# time of first observation
echo $ob_day_1 $ob_seconds_1 >> create_obs_sequence.in ;# observation interval
echo "obs_seq.in"            >> create_obs_sequence.in ;# output file name -- destined for perfect_model_obs
 
echo '&utilities_nml'                              > utilities.nml
echo '   logfilename = "create_obs_sequence.out"' >> utilities.nml
echo '   TERMLEVEL = 1   /'                       >> utilities.nml

cat utilities.nml assim_mod.nml model.nml > input.nml

./create_obs_sequence < create_obs_sequence.in

if ( $status > 0 ) then
   echo "ERROR: create_obs_sequence bombed ..."
   exit
endif

#======================================================================
# Simulate a particular observing system
# 3) Populate the observation sequences with synthetic observations
#    creates files True_State.nc, obs_seq.out
#======================================================================

echo " "
echo "Running perfect_model_obs ..."
echo " "

cat << ENDofTask03 > perfect.nml

&perfect_model_obs_nml
   async = 0,
   obs_seq_in_file_name = "obs_seq.in",
   obs_seq_out_file_name = "obs_seq.out",
   start_from_restart = .true.,
   output_restart = .true.,
   restart_in_file_name = "perfect_ics",
   restart_out_file_name = "perfect_restart",
   init_time_days = 0,
   init_time_seconds = 0,
   output_interval = 1  /
ENDofTask03

echo '&utilities_nml'                              > utilities.nml
echo '   logfilename = "perfect_obs.out"'         >> utilities.nml
echo '   TERMLEVEL = 1  /'                        >> utilities.nml

cat utilities.nml assim_mod.nml model.nml perfect.nml > input.nml

./perfect_model_obs
 
if ( $status > 0 ) then
   echo "ERROR: perfect_model_obs (synthetic) bombed ..."
   exit
else
   echo " "
   echo "perfect_model_obs (synthetic) terminated normally"
   echo " "
endif

#======================================================================
# Simulate a particular observing system
# 4) Filter
#    creates files Prior_Diag.nc, Posterior_Diag.nc
#======================================================================
# Set defaults
#======================================================================

echo " "
echo "Running filter ..."
echo " "

set async = 0
set ens_size = $NumEns
set start_from_restart = .true.
set output_restart = .true.
set obs_sequence_file_name = '"obs_seq.out"'
set restart_in_file_name = '"filter_ics"'
set restart_out_file_name = '"filter_restart"'
set init_time_days = 0
set init_time_seconds = 0
set output_state_ens_mean = .true.
set output_state_ens_spread = .true.
set num_output_ens_members = $NumEns
set output_interval = 1


echo '&utilities_nml'                            > utilities.nml
echo '   logfilename = "filter.out"'            >> utilities.nml
echo '   TERMLEVEL = 1   /'                     >> utilities.nml

echo " "                                                        > filter.nml
echo "&filter_nml"                                             >> filter.nml
echo "   async = $async ,"                                     >> filter.nml
echo "   ens_size = $ens_size ,"                               >> filter.nml
echo "   cutoff = $cutoff ,"                                   >> filter.nml
echo "   cov_inflate = $cov_inflate ,"                         >> filter.nml
echo "   start_from_restart = $start_from_restart ,"           >> filter.nml
echo "   output_restart = $output_restart ,"                   >> filter.nml
echo "   obs_sequence_file_name = $obs_sequence_file_name ,"   >> filter.nml
echo "   restart_in_file_name = $restart_in_file_name ,"       >> filter.nml
echo "   restart_out_file_name = $restart_out_file_name ,"     >> filter.nml
echo "   init_time_days = $init_time_days ,"                   >> filter.nml
echo "   init_time_seconds = $init_time_seconds ,"             >> filter.nml
echo "   output_state_ens_mean = $output_state_ens_mean ,"     >> filter.nml
echo "   output_state_ens_spread = $output_state_ens_spread ," >> filter.nml
echo "   num_output_ens_members = $num_output_ens_members ,"   >> filter.nml
echo "   output_interval = $output_interval    /"              >> filter.nml
echo " "                                                       >> filter.nml

cat utilities.nml assim_mod.nml model.nml filter.nml > input.nml

./filter

if ( $status > 0 ) then
   echo "ERROR: filter bombed ..."
else
   echo " "
   echo "filter terminated normally"
   echo " "
   \rm -f create_obs_set_def.in create_obs_sequence.in 
   \rm -f utilities.nml assim_mod.nml model.nml perfect.nml filter.nml input.nml
   \rm -f set_def.out input.nml*default
endif

#======================================================================
# Restore the existing namelists ...
#======================================================================

\mv -f ${SAVEME}/*.nml .
\mv -f ${SAVEME}/*default .
\rmdir ${SAVEME}
\rm -f nullnamelist$$.nml nullnamelist$$.nml_default

exit
