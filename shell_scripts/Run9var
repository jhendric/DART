#!/bin/csh
#
# script to run an experiment with existing intial conditions
# Interesting case #1
# identity obs for all 9 state vars, var = 1.0, obdelta =3600s
#     state vars 1,2,3  --> filter divergence
#     rerun with cov_inflate = 1.01, 1.05, 0.99 ...
#======================================================================
# Find the project and build it, if need be.
#======================================================================
                                                                                
if ( ${?DART_HOME} ) then
   echo "dart home is $DART_HOME"
   if ( -d $DART_HOME ) then
      setenv DARTHOME $DART_HOME
   else
      echo "$DART_HOME does not exist on this platform"
   endif
else if ( -d /export/data/gsp/thoar/DART ) then
   setenv DARTHOME /export/data/gsp/thoar/DART
else if ( -d /data/gsp/thoar/DART ) then
   setenv DARTHOME /data/gsp/thoar/DART
else if ( -d /home/thoar/DART ) then
   setenv DARTHOME /home/thoar/DART
else
   echo "no DART project"
   exit
endif

set Model = 9var
                                                                                
cd ${DARTHOME}/models/${Model}/work

if !( -e create_obs_set_def ) then
   csh mkmf_create_obs_set_def
   make || exit 1
endif
if !( -e create_obs_sequence ) then
   csh mkmf_create_obs_sequence
   make || exit 1
endif
if !( -e perfect_model_obs ) then
   csh mkmf_perfect_model_obs
   make || exit 1
endif
if !( -e filter ) then
   csh mkmf_filter
   make || exit 1
endif
\rm -f *.o *.mod Makefile

set SAVEME = input.nml.$$

#======================================================================
# Simulate a particular observing system:
# 1) declare an observation set definition
#======================================================================

cat << ENDofTask01 > create_obs_set_def.in
set_def.out
1           ;# number of unique observation SETS to define
3           ;# number of observations in set 1 (x,y,z)
1.0         ;# error variance for set 1, observation 1 (X)
1           ;# 1 == state variable 1 (X)
1.0         ;# error variance for set 1, observation 1 (Y)
2           ;# 1 == state variable 2 (Y)
1.0         ;# error variance for set 1, observation 1 (Z)
3           ;# 1 == state variable 3 (Z)
ENDofTask01

set obs_error_variance = 1.0
if ( ${?ObsError} ) then
   set obs_error_variance = $ObsError
endif

# This sequence block is not great. It will NOT handle
# arbitrary sets of state variables. You can observe
# the first N, or #N, but not 3,6,9 for example.
# For that, you must run interactively.

set num_obs_set_1 = 9
if ( ${?NumObsSet1} ) then
   set num_obs_set_1 = $NumObsSet1
endif
@ loopvar = 1

echo "set_def.out"                  > create_obs_set_def.in
echo "1"                           >> create_obs_set_def.in
echo $num_obs_set_1                >> create_obs_set_def.in
while ($loopvar <= $num_obs_set_1)
   echo $obs_error_variance        >> create_obs_set_def.in
   echo $loopvar                   >> create_obs_set_def.in
   @ loopvar++
end

./create_obs_set_def < create_obs_set_def.in

if ( $status > 0 ) then
   echo "ERROR: running create_obs_set_def bombed ..."
   exit
endif

#======================================================================
# Simulate a particular observing system:
# 2) declare sequences of observation sets
#======================================================================
set num_obs = 4000
set ob_day_1 = 0
set ob_seconds_1 = 3600
if ( ${?NumObs} ) then
   set num_obs = $NumObs
endif
if ( ${?ObDay1} ) then
   set ob_day_1 = $ObDay1
endif
if ( ${?ObSec1} ) then
   set ob_seconds_1 = $ObSec1
endif

echo "set_def.out"            > create_obs_sequence.in
echo "1"                     >> create_obs_sequence.in
echo $num_obs                >> create_obs_sequence.in
echo $ob_day_1 $ob_seconds_1 >> create_obs_sequence.in
echo $ob_day_1 $ob_seconds_1 >> create_obs_sequence.in
echo "obs_seq.in"            >> create_obs_sequence.in

./create_obs_sequence < create_obs_sequence.in

if ( $status > 0 ) then
   echo "ERROR: create_obs_sequence bombed ..."
   exit
endif

#======================================================================
# Simulate a particular observing system
# 3) Populate the observation sequences with synthetic observations
#    creates files True_State.nc, obs_seq.out
#======================================================================

cat << ENDofTask03 > input.nml
&model_nml
   g = 8.0,
   deltat = 0.0833333333333333
&end

&perfect_model_obs_nml
   async = .false.,
   obs_seq_in_file_name = "obs_seq.in",
   obs_seq_out_file_name = "obs_seq.out",
   start_from_restart = .true.,
   output_restart = .true.,
   restart_in_file_name = "perfect_ics",
   restart_out_file_name = "perfect_restart",
   init_time_days = 0,
   init_time_seconds = 0,
   output_interval = 1
&end
ENDofTask03

./perfect_model_obs
 
if ( $status > 0 ) then
   echo "ERROR: perfect_model_obs (synthetic) bombed ..."
   exit
else
   echo " "
   echo "perfect_model_obs (synthetic) teminated normally"
   echo " "
endif

#======================================================================
# Simulate a particular observing system
# 4) Filter
#    creates files Prior_Diag.nc, Posterior_Diag.nc
#======================================================================

set async = .false.
set ens_size = 20
set cutoff = 22222222.0
set cov_inflate = 1.00
set start_from_restart = .true.
set output_restart = .true.
set obs_sequence_file_name = '"obs_seq.out"'
set restart_in_file_name = '"filter_ics"'
set restart_out_file_name = '"filter_restart"'
set init_time_days = 0
set init_time_seconds = 0
set output_state_ens_mean = .true.
set output_state_ens_spread = .true.
set num_output_ens_members = 20
set output_interval = 1

if ( ${?NumEns} ) then
   set ens_size = $NumEns
   set num_output_ens_members = $NumEns
endif
if ( ${?CovInflate} ) then
   set cov_inflate = $CovInflate
endif
if ( ${?Cutoff} ) then
   set cutoff = $Cutoff
endif

echo " "                                                        > filter.nml
echo "&filter_nml"                                             >> filter.nml
echo "   async = $async ,"                                     >> filter.nml
echo "   ens_size = $ens_size ,"                               >> filter.nml
echo "   cutoff = $cutoff ,"                                   >> filter.nml
echo "   cov_inflate = $cov_inflate ,"                         >> filter.nml
echo "   start_from_restart = $start_from_restart ,"           >> filter.nml
echo "   output_restart = $output_restart ,"                   >> filter.nml
echo "   obs_sequence_file_name = $obs_sequence_file_name ,"   >> filter.nml
echo "   restart_in_file_name = $restart_in_file_name ,"       >> filter.nml
echo "   restart_out_file_name = $restart_out_file_name ,"     >> filter.nml
echo "   init_time_days = $init_time_days ,"                   >> filter.nml
echo "   init_time_seconds = $init_time_seconds ,"             >> filter.nml
echo "   output_state_ens_mean = $output_state_ens_mean ,"     >> filter.nml
echo "   output_state_ens_spread = $output_state_ens_spread ," >> filter.nml
echo "   num_output_ens_members = $num_output_ens_members ,"   >> filter.nml
echo "   output_interval = $output_interval "                  >> filter.nml
echo "&end"                                                    >> filter.nml
echo " "                                                       >> filter.nml

cat filter.nml >> input.nml

./filter

if ( $status > 0 ) then
   mv $SAVEME input.nml
   echo "ERROR: filter bombed ..."
   exit
else
   echo " "
   echo "filter teminated normally"
   echo " "
#   \rm -f create_obs_set_def.in create_obs_sequence.in input.nml filter.nml
#   mv $SAVEME input.nml
endif

exit
