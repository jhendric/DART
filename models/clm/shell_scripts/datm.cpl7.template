#! /bin/csh -f
if !(-d $CASEBUILD) mkdir $CASEBUILD

#------------------------------------------------------------------------------
#  determine input data files and resolution dependent variables
#  Mariana made us a custom one based on what we'd been using.
#  It uses the beta1_1_09 macros ... so we may have to check that.
#  She is not happy with the whole namelist template shell script situation,
#  and this is obviously a one-off.
#------------------------------------------------------------------------------

set file_aero = "unset"
set presaero  = .true.
set stream_paero_txt = "presaero.stream.txt"

# Error check

if ( $ATM_DOMAIN_FILE == "UNSET" ) then
   echo "ATM_DOMAIN_FILE must be set"
   exit -1
endif
if ($DATM_PRESAERO == 'none') then
   set presaero         = .false.
   set stream_paero_txt = "null"
   set year_first_aero  = 0
   set year_last_aero   = 0
   set year_align_aero  = 0
else if ($DATM_PRESAERO == 'clim_1850') then
   set file_aero = aerosoldep_monthly_1850_mean_1.9x2.5_c090421.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1
   set year_last_aero  = 1
   set year_align_aero = 1
else if ($DATM_PRESAERO == 'clim_2000') then
   set file_aero = aerosoldep_monthly_2000_mean_1.9x2.5_c090421.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1
   set year_last_aero  = 1
   set year_align_aero = 1
else if ($DATM_PRESAERO == 'trans_1850-2000') then
   set file_aero = aerosoldep_monthly_1849-2006_1.9x2.5_c090803.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1849
   set year_last_aero  = 2006
   set year_align_aero = 1849
else if ($DATM_PRESAERO == 'rcp2.6') then
   set file_aero = aerosoldep_rcp2.6_monthly_1849-2104_1.9x2.5_c100402.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1849
   set year_last_aero  = 2104
   set year_align_aero = 1849
else if ($DATM_PRESAERO == 'rcp4.5') then
   set file_aero = aerosoldep_rcp4.5_monthly_1849-2104_1.9x2.5_c100402.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1849
   set year_last_aero  = 2104
   set year_align_aero = 1849
else if ($DATM_PRESAERO == 'rcp6.0') then
   set file_aero = aerosoldep_rcp6.0_monthly_1849-2104_1.9x2.5_c100830.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1849
   set year_last_aero  = 2104
   set year_align_aero = 1849
else if ($DATM_PRESAERO == 'rcp8.5') then
   set file_aero = aerosoldep_rcp8.5_monthly_1849-2104_1.9x2.5_c100201.nc
   set path_aero = atm/cam/chem/trop_mozart_aero/aero
   set year_first_aero = 1849
   set year_last_aero  = 2104
   set year_align_aero = 1849
else if ($DATM_PRESAERO == 'pt1_pt1') then
   if ( $CLM_USRDAT_NAME != "UNSET" ) then
      set file_aero       = aerosoldep_monthly_1849-2006_${CLM_USRDAT_NAME}.nc
      set path_aero       = atm/cam/chem/trop_mozart_aero/aero
      set year_last_aero  = 2000
      set year_align_aero = 2000
   else
      if ($GRID == 1x1_brazil         ) set file_aero = aerosoldep_2000_mean_1x1_brazil_c090716.nc
      if ($GRID == 1x1_camdenNJ       ) set file_aero = aerosoldep_2000_mean_1x1_camdenNJ_c090716.nc
      if ($GRID == 1x1_tropicAtl      ) set file_aero = aerosoldep_2000_mean_1x1_tropicAtl_c090716.nc
      if ($GRID == 1x1_asphaltjungleNJ) set file_aero = aerosoldep_2000_mean_1x1_asphaltjungleNJ_c090716.nc
      if ($GRID == 1x1_mexicocityMEX  ) set file_aero = aerosoldep_2000_mean_1x1_mexicocityMEX_c090716.nc
      if ($GRID == 1x1_vancouverCAN   ) set file_aero = aerosoldep_2000_mean_1x1_vancouverCAN_c090716.nc
      if ($GRID == 1x1_urbanc_alpha   ) set file_aero = aerosoldep_2000_mean_1x1_urbanc_alpha_c090716.nc
      if ($GRID == 1x1_numaIA         ) set file_aero = aerosoldep_2000_mean_1x1_numaIA_c110124.nc
      if ($GRID == 1x1_smallvilleIA   ) set file_aero = aerosoldep_2000_mean_1x1_smallvilleIA_c110124.nc
      if ($GRID == 5x5_amazon         ) set file_aero = aerosoldep_2000_mean_5x5_amazon_c090716.nc
      set path_aero       = lnd/clm2/snicardata/
      set year_first_aero = 1
      set year_last_aero  = 1
      set year_align_aero = 1
   endif
else
   echo "ERROR: unrecognized DATM_PRESAERO value = $DATM_PRESAERO"
   exit -1
endif

if ( $DATM_PRESAERO != none)  then
   if ( $GRID == CLM_USRDAT || $ATM_GRID == reg ) then
      set file_aerop = unset
      if ( $CLM_USRDAT_NAME != "UNSET") then
         # If CLM_USRDAT_NAME set -- use it to set the tenative aerosol file
         set file_aerop = aerosoldep_monthly_1849-2006_${CLM_USRDAT_NAME}.nc
         if ( $DATM_PRESAERO == 'rcp8.5' ) set file_aerop = aerosoldep_rcp8.5_monthly_1849-2104_${CLM_USRDAT_NAME}.nc
         if ( $DATM_PRESAERO == 'rcp6'   ) set file_aerop = aerosoldep_rcp6_monthly_1849-2104_${CLM_USRDAT_NAME}.nc
         if ( $DATM_PRESAERO == 'rcp4.5' ) set file_aerop = aerosoldep_rcp4.5_monthly_1849-2104_${CLM_USRDAT_NAME}.nc
         if ( $DATM_PRESAERO == 'rcp2.6' ) set file_aerop = aerosoldep_rcp2.6_monthly_1849-2104_${CLM_USRDAT_NAME}.nc
         if ( "$file_aerop" != "unset" &&   -f "$DIN_LOC_ROOT/$path_aero/$file_aerop" ) set file_aero = $file_aerop
      endif
      if ("$file_aerop" != "unset") then
         # If aerosol file was set from above, you may also override the years
         if ( $DATM_PRESAERO == 'clim_1850') set year_first_aero = 1850
         if ( $DATM_PRESAERO == 'clim_1850') set year_last_aero  = 1850
         if ( $DATM_PRESAERO == 'clim_2000') set year_first_aero = 2000
         if ( $DATM_PRESAERO == 'clim_2000') set year_last_aero  = 2000
      endif
      if ("$GRID"  == "1x1_tropicAtl") then
         # Now specifically check for the 1x1_tropicAtl test site
         if ( $DATM_PRESAERO == 'clim_1850'      ) set file_aero = aerosoldep_monthly_1850_1x1_tropicAtl_c091026.nc
         if ( $DATM_PRESAERO == 'clim_2000'      ) set file_aero = aerosoldep_monthly_1849-2006_1x1_tropicAtl_c091026.nc
         if ( $DATM_PRESAERO == 'clim_2000'      ) set year_first_aero = 2000
         if ( $DATM_PRESAERO == 'clim_2000'      ) set year_last_aero  = 2000
         if ( $DATM_PRESAERO == 'trans_1850-2000') set file_aero = aerosoldep_monthly_1849-2006_1x1_tropicAtl_c091026.nc
      endif
   endif
endif

if ( "$DATM_PRESAERO" != 'none' && "$file_aero" == "unset" ) then
   echo "ERROR: bad DATM_PRESAERO value, input aerosol file NOT set"
   echo "       DATM_PRESAERO=$DATM_PRESAERO"
   exit -1
endif

set bsopts = "-t datm.template.streams.xml "

#==============================================================================
# Create resolved prestage data script
#==============================================================================

cat >! $CASEBUILD/datm.buildnml.csh << EOF
#! /bin/csh -f

set exedir = \$RUNDIR; cd \$exedir

#------------------------------------------------------------------------------
# specify input data files
#------------------------------------------------------------------------------
# If the user changes any input datasets - be sure they have unique filenames.
# Do not duplicate existing input file names.
# Note that streams namelist input has the form
#      streams = 'stream1.txt year_align year_first year_last ',
#                'stream2.txt year_align year_first year_last ',
#                ...
#                'streamN.txt year_align year_first year_last '
# where
# streamN.txt is the stream description file containing input stream details
# year_first  is the first year of data that will be used
# year_last   is the last  year of data that will be used
# year_align  is the model year that will be aligned with data for year_first
#------------------------------------------------------------------------------

set ATM_DOMAIN_PATH = $ATM_DOMAIN_PATH
set ATM_DOMAIN_FILE = \$ATM_DOMAIN_PATH/$ATM_DOMAIN_FILE

echo ATM_DOMAINFILE = \$ATM_DOMAIN_FILE >! \$CASEBUILD/datm.input_data_list

EOF

#----- CPLHIST 3-hourly time-averaging mode ----------------------------------------------------------
if ($DATM_MODE == "CPLHIST3HrWx" ) then

if ( "$DATM_PRESAERO" == "none"    ) echo "ERROR: DATM_PRESEARO can NOT be none for this DATM_MODE"
if ( "$DATM_PRESAERO" == "none"    ) exit 3

set year_align = $DATM_CPL_YR_ALIGN
set year_start = $DATM_CPL_YR_START
set year_end   = $DATM_CPL_YR_END
set case       = $DATM_CPL_CASE

set iradsw     = 1

cat >> $CASEBUILD/datm.buildnml.csh << EOF

set inst_counter = 1
while (\$inst_counter <= \$NINST_ATM)

set inst_string = " "
if (\$NINST_ATM > 1) then
    set inst_string = \$inst_counter
    if (\$inst_counter <= 999) set inst_string = 0\$inst_string
    if (\$inst_counter <=  99) set inst_string = 0\$inst_string
    if (\$inst_counter <=   9) set inst_string = 0\$inst_string
    set inst_string = _\$inst_string
endif

set STREAM1TXT = "CPLHIST.3Hrly.stream\${inst_string}.Solar.txt"
set STREAM2TXT = "CPLHIST.3Hrly.stream\${inst_string}.Precip.txt"
set STREAM3TXT = "CPLHIST.3Hrly.stream\${inst_string}.Other.txt"

set FFN        = "unused "

cat >! datm_atm_in\${inst_string} << EOF1
 &shr_strdata_nml
   dataMode       = 'CPLHIST'
   domainFile     = '\$ATM_DOMAIN_FILE'
   streams        = '\$STREAM1TXT $year_align $year_start $year_end ',
                    '\$STREAM2TXT $year_align $year_start $year_end ',
                    '\$STREAM3TXT $year_align $year_start $year_end ',
                    '$stream_paero_txt $year_align_aero $year_first_aero $year_last_aero'
   vectors        = 'u:v'
   mapmask        = 'nomask','nomask','nomask','nomask'
   tintalgo       = 'coszen','nearest','linear','linear'
  /
EOF1

cat >! stream1.template << EOF1
<streamstemplate>
<stream datasource="CPLHIST3Hrly.Precip">
   <comment>
      Stream description file for CPL history 3-hourly Precip data
   </comment>
   <dataSource>
      CPL
   </dataSource>
   <domainInfo>
      <variableNames>
         time        time
         xc          lon
         yc          lat
         area        area
         mask        mask
      </variableNames>
      <filePath>
         /glade/proj3/DART/CAM_DATM/2000
      </filePath>
      <fileNames>
         CAM_DATM.cpl\${inst_string}.ha2x1dx6h.2000.nc
      </fileNames>
   </domainInfo>
   <fieldInfo>
      <variableNames>
         a2x3h_Faxa_rainc     rainc
         a2x3h_Faxa_rainl     rainl
         a2x3h_Faxa_snowc     snowc
         a2x3h_Faxa_snowl     snowl
      </variableNames>
      <filePath>
         /glade/proj3/DART/CAM_DATM/2000
      </filePath>
      <tInterpAlgo>
         nearest
      </tInterpAlgo>
      <!-- Time-stamps are for the end of interval, set them to the mid-point -->
      <offset>
        -10800
      </offset>
      <fileNames>
        CAM_DATM.cpl\${inst_string}.ha2x1dx6h.2000.nc
      </fileNames>
   </fieldInfo>
</stream>
</streamstemplate>
EOF1
\$UTILROOT/Tools/build_streams -t stream1.template -s CPLHIST3Hrly.Precip \
    -b \$DATM_CLMNCEP_YR_START -e \$DATM_CLMNCEP_YR_END >! \$STREAM1TXT || exit 3
\$CASETOOLS/listfilesin_streams -input_data_list -t \$STREAM1TXT >> \$CASEBUILD/datm.input_data_list
rm stream1.template

cat >! stream2.template << EOF1
<streamstemplate>
<stream datasource="CPLHIST3Hrly.Solar">
      <comment>
         Stream description file for CPL history 3-hourly Solar data
      </comment>
      <dataSource>
         CPL
      </dataSource>
      <domainInfo>
         <variableNames>
            time    time
            doma_lon      lon
            doma_lat      lat
            doma_area    area
            doma_mask    mask
         </variableNames>
         <filePath>
            /glade/proj3/DART/CAM_DATM/2000
         </filePath>
         <fileNames>
            CAM_DATM.cpl\${inst_string}.ha2x1dx6h.2000.nc
         </fileNames>
      </domainInfo>
      <fieldInfo>
         <variableNames>
            a2x6h_Faxa_swndr     swndr
            a2x6h_Faxa_swvdr     swvdr
            a2x6h_Faxa_swndf     swndf
            a2x6h_Faxa_swvdf     swvdf
         </variableNames>
         <filePath>
            /glade/proj3/DART/CAM_DATM/2000
         </filePath>
         <tInterpAlgo>
            coszen
         </tInterpAlgo>
         <offset>
            -21600
         </offset>
         <fileNames>
           CAM_DATM.cpl\${inst_string}.ha2x1dx6h.2000.nc
         </fileNames>
      </fieldInfo>
</stream>
</streamstemplate>
EOF1
\$UTILROOT/Tools/build_streams -t stream2.template -s CPLHIST3Hrly.Solar \
    -b \$DATM_CLMNCEP_YR_START -e \$DATM_CLMNCEP_YR_END >! \$STREAM2TXT || exit 3
\$CASETOOLS/listfilesin_streams -input_data_list -t \$STREAM2TXT >> \$CASEBUILD/datm.input_data_list
rm stream2.template

cat >! stream3.template << EOF1
<streamstemplate>
<stream datasource="CPLHIST3Hrly.nonSolarNonPrecip">
      <comment>
         Stream description file for CPL history 3-hourly non-Precipitation and non-Solar data
      </comment>
      <dataSource>
         CPL
      </dataSource>
      <domainInfo>
         <variableNames>
            time    time
            doma_lon      lon
            doma_lat      lat
            doma_area    area
            doma_mask    mask
         </variableNames>
         <filePath>
            /glade/proj3/DART/CAM_DATM/2000
         </filePath>
         <fileNames>
            CAM_DATM.cpl\${inst_string}.ha2x1dx6h.2000.nc
         </fileNames>
      </domainInfo>
      <fieldInfo>
         <variableNames>
            a2x6h_Sa_z           z
            a2x6h_Sa_u           u
            a2x6h_Sa_v           v
            a2x6h_Sa_tbot        tbot
            a2x6h_Sa_ptem        ptem
            a2x6h_Sa_shum        shum
            a2x6h_Sa_pbot        pbot
            a2x6h_Faxa_lwdn      lwdn
            a2x6h_Sa_dens        dens
            a2x6h_Sa_pslv        pslv
         </variableNames>
         <filePath>
            /glade/proj3/DART/CAM_DATM/2000
         </filePath>
         <tInterpAlgo>
            linear
         </tInterpAlgo>
         <offset>
            -10800
         </offset>
         <fileNames>
            CAM_DATM.cpl\${inst_string}.ha2x1dx6h.2000.nc
         </fileNames>
      </fieldInfo>
</stream>
</streamstemplate>
EOF1
\$UTILROOT/Tools/build_streams -t stream3.template -s CPLHIST3Hrly.nonSolarNonPrecip \
    -b \$DATM_CLMNCEP_YR_START -e \$DATM_CLMNCEP_YR_END >! \$STREAM3TXT || exit 3
\$CASETOOLS/listfilesin_streams -input_data_list -t \$STREAM3TXT >> \$CASEBUILD/datm.input_data_list
rm stream3.template

@ inst_counter = \$inst_counter + 1

end

EOF

#==============================================================================
# Create prescribed aero streams if appropriate
#==============================================================================

if ($presaero == ".true.") then
cat >> $CASEBUILD/datm.buildnml.csh << EOF

cat >! $stream_paero_txt << EOF1
EOF
set bs4opts = "$bsopts -s presaero -b $year_first_aero -e $year_last_aero "
$UTILROOT/Tools/build_streams $bs4opts -p \$DIN_LOC_ROOT/$path_aero -c $file_aero -dp \$DIN_LOC_ROOT/$path_aero -do $file_aero >> $CASEBUILD/datm.buildnml.csh || exit 3
cat >> $CASEBUILD/datm.buildnml.csh << EOF
EOF1

EOF
endif

if ($presaero == ".true.") then
cat >> $CASEBUILD/datm.buildnml.csh <<EOF

\$CASETOOLS/listfilesin_streams -input_data_list -t $stream_paero_txt >> \$CASEBUILD/datm.input_data_list

EOF
endif

#==============================================================================
# Create remaining resolved namelist
#==============================================================================

cat >> $CASEBUILD/datm.buildnml.csh << EOF

set base_filename = ""
set inst_counter = 1
while (\$inst_counter <= \$NINST_ATM)

set inst_string = " "
if (\$NINST_ATM > 1) then
    set inst_string = \$inst_counter
    if (\$inst_counter <= 999) set inst_string = 0\$inst_string
    if (\$inst_counter <=  99) set inst_string = 0\$inst_string
    if (\$inst_counter <=   9) set inst_string = 0\$inst_string
    set inst_string = _\$inst_string
endif

cat >! datm_in\${inst_string} << EOF1
  &datm_nml
    atm_in = 'datm_atm_in_\${inst_string}'
    decomp = '1d'
    factorFn = '\$FFN'
    iradsw   = $iradsw
    presaero = $presaero
  /
EOF1

@ inst_counter = \$inst_counter + 1

end

EOF

#==============================================================================
#  Create script to build executable
#==============================================================================

cat >! $CASEBUILD/datm.buildexe.csh << EOF
#! /bin/csh -f

set objdir = \$OBJROOT/atm/obj; cd \$objdir
set comp = 'unknown'
if (\$COMP_INTERFACE == 'MCT' ) set comp = mct
if (\$COMP_INTERFACE == 'ESMF') set comp = esmf

#------------------------------------------------------------------------------
# Filepath: List of source code directories (in order of importance).
#------------------------------------------------------------------------------

\cat >! Filepath << EOF1
\$CASEROOT/SourceMods/src.datm
\$CODEROOT/atm/datm
\$CODEROOT/atm/datm/cpl_\$comp
EOF1

#------------------------------------------------------------------------------
# run make
#------------------------------------------------------------------------------

gmake complib -j \$GMAKE_J MODEL=datm COMPLIB=\$LIBROOT/libatm.a -f \$CASETOOLS/Makefile MACFILE=\$CASEROOT/Macros.\$MACH || exit 2

EOF

#==============================================================================
# end of script
#==============================================================================
